BBPATH .= ":${LAYERDIR}"

BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "flatpak"
BBFILE_PATTERN_flatpak := "^${LAYERDIR}/"
BBFILE_PRIORITY_flatpak = "5"

# Set a variable for easy access to the top directory of the flatpak layer.
FLATPAKBASE = '${@os.path.normpath("${LAYERDIR}/../meta-flatpak")}'

SUPPORTED_RECIPES_append = " \
    ${FLATPAKBASE}/conf/flatpak-supported-recipes.txt \
    ${FLATPAKBASE}/conf/depends-supported-recipes.txt \
"

# This should be in our distro.conf... 2.4.x is GPLv3.
#PREFERRED_VERSION_gnupg = "1.%"
PREFERRED_VERSION_gnupg = "${@bb.utils.contains('DISTRO_FEATURES', 'flatpak', \
                                                '1.%', '%', d)}"

# You can pre-declare flatpak repostories/remotes for flatpak-enabled
# images. Devices running such an image will monitor the remotes for
# for flatpak applications. Any application found will be considered for
# automatic installation and update to the device.
#
# To declare a flatpak remote, you have to
#   1) give the remote a name
#   2) provide a repository URL and GPG public key for the remote
#   3) associate a user with (IOW create a user for) the remote
#
# The variable FLATPAK_APP_REPOS lists the names of the remotes
# you want to pre-declare in the image. For every remote <r> you
# have to provide an <r>.url and <r>.key file in ${TOPDIR}/conf,
# with the HTTP URL and GPG public key for the remote as content.
#
# Similarly, you have to provide an (/etc/)passwd and (/etc/)group
# entry for every remote in the following passwd and group fragment
# files:
#
#    ${TOPDIR}/conf/flatpak-passwd
#    ${TOPDIR}/conf/flatpak-group
#
# The GECOS entry for user <u> associated with remote <r> must be
#
#    'flatpak user for <r>'
#
#
# By default FLATPAK_APP_REPOS is set to empty and no repositories are
# pre-declared.
FLATPAK_APP_REPOS ?= ""

# Pick up our passwd and group fragments for remotes if we have any
# pre-declared ones.
USERADD_UID_TABLES += "${@ d.getVar('TOPDIR') + '/conf/flatpak-passwd' \
                             if d.getVar('FLATPAK_APP_REPOS') else ''}"
USERADD_GID_TABLES += "${@ d.getVar('TOPDIR') + '/conf/flatpak-group' \
                             if d.getVar('FLATPAK_APP_REPOS') else ''}"
